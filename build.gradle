buildscript {
    ext {
        springBootVersion = '2.5.6'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE"
    }
}



// 하위 모든 프로젝트 공통 세팅
subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group 'org.ankus'
    version '0.0.1-SNAPSHOT'

    sourceCompatibility = '1.8'

    repositories {
        mavenCentral()
    }

    // 하위 모듈에서 공통으로 사용하는 세팅 추가
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-security'
        // https://mvnrepository.com/artifact/org.thymeleaf.extras/thymeleaf-extras-springsecurity5
        implementation group: 'org.thymeleaf.extras', name: 'thymeleaf-extras-springsecurity5', version: '3.0.4.RELEASE'
        compileOnly 'org.projectlombok:lombok'
        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        runtimeOnly 'org.postgresql:postgresql'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        implementation group: 'org.apache.commons', name: 'commons-csv', version: '1.5'

        // 명령어 줄 실행 구현 라이브러리
        implementation 'org.apache.commons:commons-exec:1.3'

        // jpa queryDSL
        implementation 'com.querydsl:querydsl-jpa:4.4.0'
        //Q class
        annotationProcessor "com.querydsl:querydsl-apt:4.4.0:jpa"    // querydsl JPAAnnotationProcessor 사용 지정
        annotationProcessor "jakarta.persistence:jakarta.persistence-api"	// java.lang.NoClassDefFoundError(javax.annotation.Entity) 발생 대응
        annotationProcessor "jakarta.annotation:jakarta.annotation-api"		// java.lang.NoClassDefFoundError (javax.annotation.Generated) 발생 대응

        // 외부 서버 연결
        implementation group: 'com.jcraft', name: 'jsch', version: '0.1.54'

        // dbConnect 연결
        implementation 'org.springframework.boot:spring-boot-starter-jdbc'

        // Maria DB 드라이버
        implementation 'org.mariadb.jdbc:mariadb-java-client:3.0.2-rc'

        implementation group: 'io.jsonwebtoken', name: 'jjwt', version: '0.9.1'
    }

    test {
        useJUnitPlatform()
    }
}


// 각 프로젝트에서만 사용하는 의존성 추가
project(':user') {

    // core 에서는 bootjar로 패키징 할 필요가 없으므로 추가
    bootJar.enabled = false
    jar.enabled = true

    dependencies {
    }
}

project(':jupyter') {
    bootJar { enabled = true }
    jar { enabled = false }

    dependencies {
        implementation project(':user') // 없으면 login 화면 안보임
        compileOnly project(':user') // 컴파일 시 user모듈(java) 가져옴
    }
}

project(':workflow') {
    bootJar { enabled = true }
    jar { enabled = false }

    dependencies {
        implementation project(':user') // 없으면 login 화면 안보임
        compileOnly project(':user') // 컴파일 시 user모듈(java) 가져옴
    }
}